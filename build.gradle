buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.2.5'
    id 'org.jetbrains.kotlin.jvm' version '1.1.1'
    id "de.undercouch.download" version "3.2.0"
}

def isCi = System.env.CI != null

allprojects {
    apply plugin: 'idea'
    idea {
        module {
            generatedSourceDirs += file('src/gen')
        }
    }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        version ideaVersion
        downloadSources !isCi
        updateSinceUntilBuild = false
        instrumentCode = false
//        alternativeIdePath = "debugger/lib/clion-$clionVersion"

        publishPlugin {
            username publishUsername
            password publishPassword
            channels publishChannel
        }
    }

    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    compileKotlin {
        kotlinOptions {
            languageVersion = '1.1'
            // BACKCOMPAT: 2016.3
            apiVersion = '1.0'
        }
    }

    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
    sourceSets {
        main {
            java.srcDirs += 'src/gen'
        }
    }

    test {
        testLogging {
            events 'skipped', 'failed'
            exceptionFormat = 'full'
        }

        beforeSuite { suite ->
            if (suite.className != null) {
                println()
                println(suite.className)
            }
        }
        afterTest { desc, result ->
            def c = '.'
            if (result.resultType == TestResult.ResultType.FAILURE) {
                c = 'X'
            } else if (result.resultType == TestResult.ResultType.SKIPPED) {
                c = 'S'
            }
            print(c)
            System.out.flush()
        }
        afterSuite { println() }
    }

    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/jetbrains/markdown' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        compileOnly('com.github.JetBrains:Grammar-Kit:34fd65b92af957c25d940b828d81ab3ba9b398b0') {
            exclude group: 'org.jetbrains.plugins'
            exclude module: 'idea'
        }

        testCompile 'junit:junit:4.+'
        testCompile 'org.assertj:assertj-core:3.2.0'
    }
}

project(':') {
    version = "0.1.0.$buildNumber" + (publishChannel?.trim() ? "-$publishChannel" : "")
    intellij { pluginName 'intellij-rust' }

    sourceSets {
        main {
            kotlin.srcDirs += 'debugger/src/main/kotlin'
            compileClasspath += files("debugger/lib/clion-$clionVersion/lib/clion.jar")
        }
    }
}

project(':toml') {
    version = "0.0.1.$buildNumber" + (publishChannel?.trim() ? "-$publishChannel" : "")
    intellij { pluginName 'intellij-toml' }
}

test {
    useJUnit {
        excludeCategories 'org.rust.Performance'
    }
}


task downloadClion(type: de.undercouch.gradle.tasks.download.Download) {
    src "https://download.jetbrains.com/cpp/CLion-${clionVersion}.tar.gz"
    dest new File(project.projectDir, "debugger/lib/clion-${clionVersion}.tar.gz")
    overwrite false
}

task unpackClion(dependsOn: downloadClion, type: Copy) {
    from tarTree("debugger/lib/clion-${clionVersion}.tar.gz")
    into new File(project.projectDir, "debugger/lib")
}
compileKotlin.dependsOn unpackClion

task performanceTest(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
    useJUnit {
        includeCategories 'org.rust.Performance'
        reports.html.destination = "$buildDir/reports/performanceTests"
    }

    testLogging {
        events 'passed', 'skipped', 'failed'
        showStandardStreams = true
        exceptionFormat = 'full'
    }

    outputs.upToDateWhen { false } // always execute task, even if already executed.
}

check.dependsOn performanceTest


dependencies {
    compile('org.jetbrains:markdown:0.1.12') {
        exclude module: 'kotlin-runtime'
        exclude module: 'kotlin-stdlib'
    }
}

lexerTask(project, 'RustLexer', 'org/rust/lang/core/lexer')
lexerTask(project, 'RustDocHighlightingLexer', 'org/rust/lang/doc/lexer')
parserTask(project, 'RustParser', 'org/rust/lang/core/psi')

project(':toml') {
    lexerTask(project, 'TomlLexer', 'org/toml/lang/core/lexer')
    parserTask(project, 'TomlParser', 'org/toml/lang/core/psi')
}


static def codegenTask(project, task) {
    project.compileKotlin.dependsOn task
    project.compileTestKotlin.dependsOn task
    return task
}

def lexerTask(project, lexerName, pkg) {
    return codegenTask(project, tasks.create("generate${lexerName}", JavaExec) {
        def src = "$project.projectDir/src/main/grammars/${lexerName}.flex"
        def dst = "$project.projectDir/src/gen/$pkg"

        main = 'jflex.Main'
        classpath = files('lib/jflex/jflex-1.7.0-SNAPSHOT.jar')

        args = ['--skel', 'lib/jflex/idea-flex.skeleton',
                '-d', dst,
                src
        ]

        inputs.file file(src)
        outputs.dir file("$dst/_${lexerName}.java")
    })
}

def parserTask(project, parserName, pkg) {
    return codegenTask(project, tasks.create("generate${parserName}", JavaExec) {
        def dstRoot = "$project.projectDir/src/gen"
        def src = "$project.projectDir/src/main/grammars/${parserName}.bnf"
        def dst = "$dstRoot/$pkg"
        doFirst {
            delete file(dst)
        }

        main = 'org.intellij.grammar.Main'
        classpath(configurations.compileOnly, configurations.compile)

        args = [dstRoot, file(src)]

        inputs.file file(src)
        outputs.dir dst
    })
}
